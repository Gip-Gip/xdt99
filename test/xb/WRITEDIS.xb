100 REM WRITE REFERENCE RECORDS

101 REM VARIABLE RECORDS

111 OPEN #1:"DSK2.V16",DISPLAY,OUTPUT,VARIABLE 16
112 COUNT=50 :: LENGTH=16 :: GOSUB 1000
113 CLOSE #1

121 OPEN #1:"DSK2.V126",DISPLAY,OUTPUT,VARIABLE 126
122 COUNT=10 :: LENGTH=126 :: GOSUB 1000
123 CLOSE #1

131 OPEN #1:"DSK2.V127",DISPLAY,OUTPUT,VARIABLE 127
132 COUNT=10 :: LENGTH=127 :: GOSUB 1000
133 CLOSE #1

141 OPEN #1:"DSK2.V128",DISPLAY,OUTPUT,VARIABLE 128
142 COUNT=10 :: LENGTH=128 :: GOSUB 1000
143 CLOSE #1

151 OPEN #1:"DSK2.V254",DISPLAY,OUTPUT,VARIABLE 254
152 COUNT=10 :: LENGTH=254 :: GOSUB 1000
153 CLOSE #1

161 OPEN #1:"DSK2.V255",DISPLAY,OUTPUT,VARIABLE 255
162 COUNT=10 :: LENGTH=255 :: GOSUB 1000
163 CLOSE #1

200 REM FIXED RECORDS

211 OPEN #1:"DSK2.F16",DISPLAY,OUTPUT,FIXED 16
212 COUNT=50 :: LENGTH=16 :: GOSUB 1000
213 CLOSE #1

221 OPEN #1:"DSK2.F127",DISPLAY,OUTPUT,FIXED 127
222 COUNT=10 :: LENGTH=127 :: GOSUB 1000
223 CLOSE #1

231 OPEN #1:"DSK2.F128",DISPLAY,OUTPUT,FIXED 128
232 COUNT=10 :: LENGTH=128 :: GOSUB 1000
233 CLOSE #1

241 OPEN #1:"DSK2.F129",DISPLAY,OUTPUT,FIXED 129
242 COUNT=10 :: LENGTH=129 :: GOSUB 1000
243 CLOSE #1

251 OPEN #1:"DSK2.F254",DISPLAY,OUTPUT,FIXED 254
252 COUNT=10 :: LENGTH=254 :: GOSUB 1000
253 CLOSE #1

261 OPEN #1:"DSK2.F255",DISPLAY,OUTPUT,FIXED 255
262 COUNT=10 :: LENGTH=255 :: GOSUB 1000
263 CLOSE #1

300 REM VARIABLE LENGTH

301 L$="" :: FOR I=64 TO 126 :: L$=L$&CHR$(I) :: NEXT I
302 L$=L$&"*"&L$&"*"&L$&"*"&L$
303 PRINT LEN(L$)

310 DATA 64,63,64,61,62,0,63,64,62,-1
311 OPEN #1:"DSK2.V64V",DISPLAY,OUTPUT,VARIABLE 64 :: GOSUB 2000 :: CLOSE #1

320 DATA 64,63,64,61,62,0,63,64,62,-1
321 OPEN #1:"DSK2.F64V",DISPLAY,OUTPUT,FIXED 64 :: GOSUB 2000 :: CLOSE #1

330 DATA 255,-1
331 OPEN #1:"DSK2.V255V1",DISPLAY,OUTPUT,VARIABLE 255 :: GOSUB 2000 :: CLOSE #1

340 DATA 255,127,126,-1
341 OPEN #1:"DSK2.V255V2",DISPLAY,OUTPUT,VARIABLE 255 :: GOSUB 2000 :: CLOSE #1

350 DATA 255,127,127,-1
351 OPEN #1:"DSK2.V255V3",DISPLAY,OUTPUT,VARIABLE 255 :: GOSUB 2000 :: CLOSE #1

360 DATA 255,127,127,127,255,-1
361 OPEN #1:"DSK2.V255V4",DISPLAY,OUTPUT,VARIABLE 255 :: GOSUB 2000 :: CLOSE #1

370 DATA 255,127,127,255,255,255,-1
371 OPEN #1:"DSK2.V255V5",DISPLAY,OUTPUT,VARIABLE 255 :: GOSUB 2000 :: CLOSE #1

400 REM SPECIAL RECORDS

411 OPEN #1:"DSK2.V1",DISPLAY,OUTPUT,VARIABLE 1
412 PRINT #1:"1":"2":" ":"":"5":"":""
413 CLOSE #1

421 OPEN #1:"DSK2.F1",DISPLAY,OUTPUT,FIXED 1
422 PRINT #1:"1":"2":" ":"":"5":"":""
423 CLOSE #1

999 END

1000 REM WRITE FIXLEN DATA
1010 FOR I=1 TO COUNT
1020 L$="" :: S=0
1030 FOR J=1 TO LENGTH-1
1040 C=INT(RND*80)
1050 L$=L$&CHR$(C+48) :: S=S+C :: IF S>=80 THEN S=S-80
1060 NEXT J
1070 IF S>0 THEN S=80-S
1080 L$=L$&CHR$(S+48)
1090 PRINT L$
1100 PRINT #1:L$
1110 NEXT I
1120 RETURN

2000 REM WRITE VARLEN DATA
2010 I=0
2020 READ L :: I=I+1
2030 IF L=0 THEN PRINT #1 :: GOTO 2020
2040 IF L<>-1 THEN PRINT #1:"!";SEG$(L$,1,L-2);CHR$(48+I) :: GOTO 2020
2050 RETURN
